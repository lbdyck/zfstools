/* --------------------  rexx procedure  -------------------- *
 * Name:      zfsUnMNT                                        *
 *                                                            *
 * Function:  UnMount an existing zFS from a mount point      *
 *                                                            *
 * Syntax:    %zfsumnt                                        *
 *                                                            *
 * Author:    Lionel B. Dyck                                  *
 *                                                            *
 * Notes:     This function requires the user have            *
 *            SuperUser authority AND that the zFS            *
 *            be mounted.                                     *
 *                                                            *
 * History:                                                   *
 *            07/07/24 - Replace STEMView with STEMEdit       *
 *            05/19/16 - Support z/OS 2.1 and 2.2+            *
 *            05/18/16 - Call the IBM Mount Table by          *
 *                       File System dialog                   *
 *            05/16/16 - Creation                             *
 *                                                            *
 * ---------------------------------------------------------- */
/* --------------------- *
 * Check version of ISPF *
 * --------------------- */
 Address ISPExec
 'vget (zenvir)'
 parse var zenvir . "."ver"MVS" .

 if ver > 1 then do
    Address TSO
    address syscall 'geteuid'
    myeuid=retval
    Address syscall "seteuid 0"
    Address ISPExec ,
    'Select cmd(%zfsibm FS1)'
    Address syscall "seteuid" myeuid
    exit 0
    end

/* -------------------- *
 * For z/OS 2.1 do this *
 * -------------------- */
 Address ISPExec
 do forever
    "Display Panel(zfsumnt)"
    if rc > 4 then exit 0
    call do_it
    Address ISPExec
    end

Do_It:
 Address TSO
 address syscall 'geteuid'
 myeuid=retval
 Address syscall "seteuid 0"
 option = '/usr/sbin/unmount' zfspath
 rc = bpxwunix(option,,stdout.,stderr.)
 rc = syscalls("OFF")
 Address syscall "seteuid" myeuid
 Address ISPExec 'vget (zfsview)'
 if zfsview = '' then zfsview = 'Browse'
 if stdout.0 > 0 then
    CALL stemedit zfsview,'stdout.',,,"zfs Unmount" option
 if stderr.0 > 0 then
    CALL stemedit zfsview,'stderr.',,,"zfs Unmount error" option
 if stdout.0 + stderr.0 = 0 then do
    zedsmsg = ""
    zedlmsg = zfspath "was successfully unmounted."
    Address ISPExec "Setmsg msg(isrz001)"
    end
 Return
