/* --------------------  rexx procedure  -------------------- *
 * Name:      ZFSADMSU                                        *
 *                                                            *
 * Function:  Display results of various ZFSADM authorzied    *
 *            commands                                        *
 *                                                            *
 * Syntax:    %zfsadmsu option                                *
 *                                                            *
 * Author:    Lionel B. Dyck                                  *
 *                                                            *
 * History:                                                   *
 *            06/23/25 - Remove need for STEMEDIT             *
 *            07/07/24 - Replace STEMView with STEMEdit       *
 *            05/16/16 - Creation                             *
 *                                                            *
 * ---------------------------------------------------------- */
 parse arg option
 option = translate(option,"abcdefghijklmnopqrstuvwxyz", ,
                    "ABCDEFGHIJKLMNOPQRSTUVWXYZ")
 address syscall 'geteuid'
 myeuid=retval
 Address syscall "seteuid 0"
 rc = bpxwunix("zfsadm" option,,stdout.,stderr.)
 rc = syscalls("OFF")
 Address syscall "seteuid" myeuid
 Address ISPExec 'vget (zfsview)'
 dd = 'zfs' || random(9999)
 'alloc f('dd') unit(3390) spa(15,15) tr' ,
   'recfm(v b) lrecl(255) blksize(0)'
 'execio * diskw' dd '(stem stdout.'
 if stderr.0 > 0 then do
    sep.0 = 3
    sep.1 = ' '; sep.2 = copies('-',70); sep.3 = ' '
    'execio * diskw' dd '(stem sep.'
    'execio * diskw' dd '(stem stderr.'
    end
 'execio 0 diskw' dd '(finis'

 Address ISPExec
  'lminit dataid(ddb) ddname('dd')'
  zfsview 'dataid('ddb')'
  'lmfree dataid('ddb')'
  Address TSO ,
  'free f('dd')'
