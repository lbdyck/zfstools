  /* --------------------  rexx procedure  -------------------- *
  * Name:      zfsalloc                                        *
  *                                                            *
  * Function:  Allocate and Format a new zFS dataset           *
  *                                                            *
  * Syntax:    %zfsalloc                                       *
  *                                                            *
  * Author:    Lionel B. Dyck                                  *
  *                                                            *
  * History:                                                   *
  *            03/14/25 - Cleanup t. variable                  *
  *                     - change from define to zfsadm         *
  *            12/10/24 - Change sysdsn to listc ent           *
  *            07/07/24 - Correct stemedit title               *
  *                     - Replace STEMView with STEMEdit       *
  *            03/27/24 - Support Cyl and Megabyte allocations *
  *            05/17/21 - Add volser option                    *
  *            08/06/18 - Bug fix for dataclass                *
  *            05/17/16 - Add Mgmt                             *
  *            05/16/16 - Add Dataclas and Storclas            *
  *            05/13/16 - Updated to discard usscmd and to     *
  *                       use bpxwunix                         *
  *            05/13/16 - Creation                             *
  *                                                            *
  * ---------------------------------------------------------- */
  parse value '' with null

  rc = syscalls("ON")

  Address ISPExec
  do forever
    "Display Panel(zfsalloc)"
    if rc > 4 then exit 0
    call do_it
    Address ISPExec
  end

Do_It:
  /* ---------------------------------------------------- *
  * Verify that the requested zFS does not already exist *
  * ---------------------------------------------------- */
  call outtrap 'x.'
  Address TSO ,
    'listc ent('zfsdsn')'
  lrc = rc
  call outtrap 'off'
  if lrc = 0 then do
    zedsmsg = ''
    zedlmsg = "Error:" zfsdsn ,
      "already exists - specify a unique dataset name."
    "Setmsg msg(isrz001)"
    return
  end

  /* -------------- *
  * Define the zFS *
  * -------------- */
  Select
    When atype = 'C' then stype = "-cylinders"
    When atype = 'M' then stype = "-megabytes"
    otherwise stype = '-cylinders'
    end
  parse value '' with dopt sopt mopt
  if dcls /= null then dopt = "-dataclass" dcls
  if mcls /= null then mopt = "-managementlass" mcls
  if scls /= null then sopt = "-storageclass" scls
  if left(zfsdsn,1) = "'"
  then zfsdsnc = substr(zfsdsn,2,length(zfsdsn)-2)
  else do
    if sysvar('syspref') = null
    then zfsdsnc = zfsdsn
    else zfsdsnc = sysvar('syspref')'.'zfsdsn
  end
  if zfsvol = null then vol = null
  else vol = '-volumes' zfsvol
  unixcmd = 'zfsadm define -aggregate' zfsdsnc dopt mopt sopt ,
        stype palc salc vol

  unixcmd = space(unixcmd)
  drop stdout. stderr.
  rc = bpxwunix(unixcmd,,stdout.,stderr.)
  reprc = rc

  c = 3
  dashlen = length(unixcmd)
  cmd.1 = copies('-',dashlen)
  cmd.2 = unixcmd
  cmd.3 = copies('-',dashlen)
  do i = 1 to stdout.0
     c = c + 1
     cmd.c = stdout.i
     end
  cmd.0 = c

  Address ISPExec 'vget (zfsview)'
  if zfsview = '' then zfsview = 'Browse'
  CALL stemedit zfsview,'cmd.',,,"zfs allocation"
  if reprc > 0 then do
  CALL stemedit zfsview,'stderr.',,,"zfs allocation error"
  return
  end

  /* -------------------------- *
  * Now Format the zFS Dataset *
  * -------------------------- */
  unixcmd = "zfsadm format -aggr" zfsdsnc "-version5"
  drop stdout. stderr.
  rc = bpxwunix(unixcmd,,stdout.,stderr.)
  reprc = rc

  if zfsview = '' then zfsview = 'Browse'
  CALL stemedit zfsview,'stdout.',,,"zfs allocation"

  return
