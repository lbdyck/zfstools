  /* --------------------  rexx procedure  -------------------- *
  * Name:      zfsalloc                                        *
  *                                                            *
  * Function:  Allocate and Format a new zFS dataset           *
  *                                                            *
  * Syntax:    %zfsalloc                                       *
  *                                                            *
  * Author:    Lionel B. Dyck                                  *
  *                                                            *
  * History:                                                   *
  *            06/23/25 - Remove need for STEMEDIT             *
  *            03/14/25 - Cleanup t. variable                  *
  *                     - change from define to zfsadm         *
  *            12/10/24 - Change sysdsn to listc ent           *
  *            07/07/24 - Correct stemedit title               *
  *                     - Replace STEMView with STEMEdit       *
  *            03/27/24 - Support Cyl and Megabyte allocations *
  *            05/17/21 - Add volser option                    *
  *            08/06/18 - Bug fix for dataclass                *
  *            05/17/16 - Add Mgmt                             *
  *            05/16/16 - Add Dataclas and Storclas            *
  *            05/13/16 - Updated to discard usscmd and to     *
  *                       use bpxwunix                         *
  *            05/13/16 - Creation                             *
  *                                                            *
  * ---------------------------------------------------------- */
  parse value '' with null

  rc = syscalls("ON")

  Address ISPExec
  do forever
    "Display Panel(zfsalloc)"
    if rc > 4 then exit 0
    call do_it
    Address ISPExec
  end

Do_It:
  /* ---------------------------------------------------- *
  * Verify that the requested zFS does not already exist *
  * ---------------------------------------------------- */
  drop stdout. stderr.
  call outtrap 'x.'
  Address TSO ,
    'listc ent('zfsdsn')'
  lrc = rc
  call outtrap 'off'
  if lrc = 0 then do
    zedsmsg = ''
    zedlmsg = "Error:" zfsdsn ,
      "already exists - specify a unique dataset name."
    "Setmsg msg(isrz001)"
    return
  end

  /* -------------- *
  * Define the zFS *
  * -------------- */
  Select
    When atype = 'C' then stype = "-cylinders"
    When atype = 'M' then stype = "-megabytes"
    otherwise stype = '-cylinders'
  end
  parse value '' with dopt sopt mopt
  if dcls /= null then dopt = "-dataclass" dcls
  if mcls /= null then mopt = "-managementlass" mcls
  if scls /= null then sopt = "-storageclass" scls
  if left(zfsdsn,1) = "'"
  then zfsdsnc = substr(zfsdsn,2,length(zfsdsn)-2)
  else do
    if sysvar('syspref') = null
    then zfsdsnc = zfsdsn
    else zfsdsnc = sysvar('syspref')'.'zfsdsn
  end
  if zfsvol = null then vol = null
  else vol = '-volumes' zfsvol
  unixcmd = 'zfsadm define -aggregate' zfsdsnc dopt mopt sopt ,
    stype palc salc vol

  unixcmd = space(unixcmd)
  drop stdout. stderr.
  rc = bpxwunix(unixcmd,,stdout.,stderr.)
  reprc = rc

  c = 3
  dashlen = length(unixcmd)
  cmd.1 = copies('-',dashlen)
  cmd.2 = unixcmd
  cmd.3 = copies('-',dashlen)
  do i = 1 to stdout.0
    c = c + 1
    cmd.c = stdout.i
  end
  cmd.0 = c


  /* ------------------------------- *
  | View the results of the command |
  * ------------------------------- */
  Address ISPExec 'vget (zfsview)'
  if zfsview = '' then zfsview = 'Browse'

  Address TSO
  dd = 'zfs' || random(9999)
  'alloc f('dd') unit(3390) spa(15,15) tr' ,
    'recfm(v b) lrecl(255) blksize(0)'
  'execio * diskw' dd '(finis stem cmd.'

  Address ISPExec
  'lminit dataid(ddb) ddname('dd')'
  zfsview 'dataid('ddb')'
  'lmfree dataid('ddb')'
  Address TSO ,
    'free f('dd')'
  if reprc > 0 then return

  /* -------------------------- *
  * Now Format the zFS Dataset *
  * -------------------------- */
  unixcmd = "zfsadm format -aggr" zfsdsnc "-version5"
  drop stdout. stderr.
  rc = bpxwunix(unixcmd,,stdout.,stderr.)
  reprc = rc

  if zfsview = '' then zfsview = 'Browse'

  Address TSO
  'alloc f('dd') unit(3390) spa(15,15) tr' ,
    'recfm(v b) lrecl(255) blksize(0)'
  'execio * diskw' dd '(stem stdout.'
  if stderr.0 > 0 then do
    sep.0 = 3
    sep.1 = ' '; sep.2 = copies('-',70); sep.3 = ' '
    'execio * diskw' dd '(stem sep.'
    'execio * diskw' dd '(stem stderr.'
  end
  'execio 0 diskw' dd '(finis'

  Address ISPExec
  'lminit dataid(ddb) ddname('dd')'
  zfsview 'dataid('ddb')'
  'lmfree dataid('ddb')'
  Address TSO ,
    'free f('dd')'

  return
