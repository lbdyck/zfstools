/* --------------------  rexx procedure  -------------------- *
 * Name:      zfsMnt                                          *
 *                                                            *
 * Function:  Mount an existing zFS to a mount point          *
 *                                                            *
 * Syntax:    %zfsmnt                                         *
 *                                                            *
 * Author:    Lionel B. Dyck                                  *
 *                                                            *
 * Notes:     This function requires the user have            *
 *            SuperUser authority                             *
 *                                                            *
 * History:                                                   *
 *            07/07/24 - Replace STEMView with STEMEdit       *
 *            05/19/16 - Enable support for z/OS 2.1 and 2.2+ *
 *            05/18/16 - Changed to use IBM's Mount dialog    *
 *            05/17/16 - Add Read Only/Read Write flag        *
 *            05/16/16 - Creation                             *
 *                                                            *
 * ---------------------------------------------------------- */

/* --------------------- *
 * Check version of ISPF *
 * --------------------- */
 Address ISPExec
 'vget (zenvir)'
 parse var zenvir . "."ver"MVS" .

 if ver > 1 then do
    Address TSO
    address syscall 'geteuid'
    myeuid=retval
    Address syscall "seteuid 0"
    Address ISPExec ,
       'select pgm(isrrcl) parm(FS4)'
    Address syscall "seteuid" myeuid
    exit 0
    end
 parse value '' with null hlq

/* -------------------- *
 * For z/OS 2.1 do this *
 * -------------------- */
 Address ISPExec
 do forever
    "Display Panel(zfsmnt)"
    if rc > 4 then exit 0
    call do_it
    Address ISPExec
    end

Do_It:
/* ------------------------------------ *
 * Verify that the requested zFS exists *
 * ------------------------------------ */
 if left(zfsdsn,1) /= "'" then do
    if sysvar('syspref') /= null then hlq = sysvar('syspref')
    if hlq = null then
       hlq = sysvar('sysuid')
    zfsdsn = "'"hlq"."zfsdsn"'"
    end
 if sysdsn(zfsdsn) /= "OK" then do
    zedsmsg = ''
    zedlmsg = 'Error:' zfsdsn "does not exist or it is already mounted."
    "Setmsg msg(isrz001)"
    return
    end

 if mntrorw = 'Y' then flag = '-rv'
                  else flag = '-v'

 Address TSO
 address syscall 'geteuid'
 myeuid=retval
 Address syscall "seteuid 0"
 option = '/usr/sbin/mount -t zfs' flag ' -f' zfsdsn zfspath
 rc = bpxwunix(option,,stdout.,stderr.)
 rc = syscalls("OFF")
 Address syscall "seteuid" myeuid

/* ------------------------------- *
 | View the results of the command |
 * ------------------------------- */
 Address ISPExec 'vget (zfsview)'
 if zfsview = '' then zfsview = 'Browse'

 dd = 'zfs' || random(9999)
 'alloc f('dd') unit(3390) spa(15,15) tr' ,
   'recfm(v b) lrecl(255) blksize(0)'
 'execio * diskw' dd '(stem stdout.'
 if stderr.0 > 0 then do
    sep.0 = 3
    sep.1 = ' '; sep.2 = copies('-',70); sep.3 = ' '
    'execio * diskw' dd '(stem sep.'
    'execio * diskw' dd '(stem stderr.'
    end
 'execio 0 diskw' dd '(finis'

 Address ISPExec
  'lminit dataid(ddb) ddname('dd')'
  zfsview 'dataid('ddb')'
  'lmfree dataid('ddb')'
  Address TSO ,
  'free f('dd')'

 if stdout.0 + stderr.0 = 0 then do
    zedsmsg = ''
    zedlmsg = zfsdsn "was successfully mounted on:" zfspath
    Address ISPExec "Setmsg msg(isrz001)"
    end
 Return
