/* --------------------  rexx procedure  -------------------- *
 * Name:      zfsMnt                                          *
 *                                                            *
 * Function:  Mount an existing zFS to a mount point          *
 *                                                            *
 * Syntax:    %zfsmnt                                         *
 *                                                            *
 * Author:    Lionel B. Dyck                                  *
 *                                                            *
 * Notes:     This function requires the user have            *
 *            SuperUser authority                             *
 *                                                            *
 * History:                                                   *
 *            07/07/24 - Replace STEMView with STEMEdit       *
 *            05/19/16 - Enable support for z/OS 2.1 and 2.2+ *
 *            05/18/16 - Changed to use IBM's Mount dialog    *
 *            05/17/16 - Add Read Only/Read Write flag        *
 *            05/16/16 - Creation                             *
 *                                                            *
 * ---------------------------------------------------------- */

/* --------------------- *
 * Check version of ISPF *
 * --------------------- */
 Address ISPExec
 'vget (zenvir)'
 parse var zenvir . "."ver"MVS" .

 if ver > 1 then do
    Address TSO
    address syscall 'geteuid'
    myeuid=retval
    Address syscall "seteuid 0"
    Address ISPExec ,
       'select pgm(isrrcl) parm(FS4)'
    Address syscall "seteuid" myeuid
    exit 0
    end
 parse value '' with null hlq

/* -------------------- *
 * For z/OS 2.1 do this *
 * -------------------- */
 Address ISPExec
 do forever
    "Display Panel(zfsmnt)"
    if rc > 4 then exit 0
    call do_it
    Address ISPExec
    end

Do_It:
/* ------------------------------------ *
 * Verify that the requested zFS exists *
 * ------------------------------------ */
 if left(zfsdsn,1) /= "'" then do
    if sysvar('syspref') /= null then hlq = sysvar('syspref')
    if hlq = null then
       hlq = sysvar('sysuid')
    zfsdsn = "'"hlq"."zfsdsn"'"
    end
 if sysdsn(zfsdsn) /= "OK" then do
    zedsmsg = ''
    zedlmsg = 'Error:' zfsdsn "does not exist or it is already mounted."
    "Setmsg msg(isrz001)"
    return
    end

 if mntrorw = 'Y' then flag = '-rv'
                  else flag = '-v'

 Address TSO
 address syscall 'geteuid'
 myeuid=retval
 Address syscall "seteuid 0"
 option = '/usr/sbin/mount -t zfs' flag ' -f' zfsdsn zfspath
 rc = bpxwunix(option,,stdout.,stderr.)
 rc = syscalls("OFF")
 Address syscall "seteuid" myeuid
 Address ISPExec 'vget (zfsview)'
 if zfsview = null then zfsview = 'Browse'
 if stdout.0 > 0 then
    CALL stemedit zfsview,'stdout.',,,"zfs mount" option
 if stderr.0 > 0 then
    CALL stemedit zfsview,'stderr.',,,"zfs mount error" option
 if stdout.0 + stderr.0 = 0 then do
    zedsmsg = ''
    zedlmsg = zfsdsn "was successfully mounted on:" zfspath
    Address ISPExec "Setmsg msg(isrz001)"
    end
 Return
