/* --------------------  rexx procedure  -------------------- *
 * Name:      ZFSADM                                          *
 *                                                            *
 * Function:  Display results of various ZFSADM queries       *
 *                                                            *
 * Syntax:    %zfsadm option                                  *
 *                                                            *
 * Author:    Lionel B. Dyck                                  *
 *                                                            *
 * History:                                                   *
 *            06/25/25 - Display command in report            *
 *            06/23/25 - Remove need for STEMEDIT             *
 *                     - check for null stems before viewing  *
 *            07/07/24 - Replace STEMView with STEMEdit       *
 *            05/19/16 - Check fsinfo for z/OS 2.2 or newer   *
 *            05/16/16 - Add view option                      *
 *            05/13/16 - Creation                             *
 *                                                            *
 * ---------------------------------------------------------- */
 parse arg option
 option = translate(option,"abcdefghijklmnopqrstuvwxyz", ,
                    "ABCDEFGHIJKLMNOPQRSTUVWXYZ")

/* ------------------------------------- *
 * Test for z/OS 2.2 or newer for fsinfo *
 * ------------------------------------- */
 zlt = 0
 if pos('fsinfo',option) > 0 then zlt = 1
 if pos('ctkc',option)   > 0 then zlt = 1
 if pos('stkm',option)   > 0 then zlt = 1
 if pos('sui',option)    > 0 then zlt = 1
 if zlt = 1 then do
    parse value mvsvar('sysmvs') with 'SP'level
    if level < '7.2.2' then do
       zedsmsg = ''
       zedlmsg = 'This function is not supported on this level' ,
                 'of z/OS.  Requires 2.2 or higher.'
       Address ISPExec 'Setmsg msg(isrz001)'
       exit 4
       end
    end

/* ------------------------- *
 * Invoke the zfsadm service *
 * ------------------------- */
 rc = syscalls("ON")
 rc = bpxwunix("zfsadm" option,,stdout.,stderr.)
 rc = syscalls("OFF")

  /* ------------------- *
   | Clean up null stems |
   * ------------------- */
 do i = 1 to stdout.0
    if length(stdout.i) = 0 then stdout.i = ' '
    end

/* ------------------------------- *
 | View the results of the command |
 * ------------------------------- */
 Address ISPExec 'vget (zfsview)'
 if zfsview = '' then zfsview = 'Browse'

 dd = 'zfs' || random(9999)
 'alloc f('dd') unit(3390) spa(15,15) tr' ,
   'recfm(v b) lrecl(255) blksize(0)'
 c.1 = 'zfsadm' option
 c.2 = copies('-',72)
 c.0 = 2
 'execio * diskw' dd '(stem c.'
 'execio * diskw' dd '(stem stdout.'
 if stderr.0 > 0 then do
    sep.0 = 3
    sep.1 = ' '; sep.2 = copies('-',70); sep.3 = ' '
    'execio * diskw' dd '(stem sep.'
    'execio * diskw' dd '(stem stderr.'
    end
 'execio 0 diskw' dd '(finis'

 Address ISPExec
  'lminit dataid(ddb) ddname('dd')'
  zfsview 'dataid('ddb')'
  'lmfree dataid('ddb')'
  Address TSO ,
  'free f('dd')'
