  /* --------------------  rexx procedure  -------------------- *
  | Name:      ZFSAMNT                                         |
  |                                                            |
  | Function:  Report all AutoMounted Filesystems              |
  |                                                            |
  | Syntax:    %ZFSAMNT                                        |
  |                                                            |
  | Usage Notes: Requires that automount be active             |
  |                                                            |
  | Dependencies:                                              |
  |                                                            |
  | Author:    Lionel B. Dyck                                  |
  |                                                            |
  | History:  (most recent on top)                             |
  |            2024/10/08 LBD - Convert from CKAMNT and add to |
  |                             the ZFSTools family            |
  |            2024/10/07 LBD - Improve test if no automounts  |
  |            2024/10/06 LBD - Report if not automounts       |
  |            2024/10/03 LBD - Creation                       |
  |                                                            |
  * ---------------------------------------------------------- */
  parse value '' with null
  env.1 = 'HOME=$PWD'
  env.2 = 'PATH=/bin:/usr/sbin'
  env.0 = 2
  cmd = 'automount -q | grep /'
  x = bpxwunix(cmd,,s.,e.,env.)
  if s.0 = 0 then do
      say 'No automounts detected.'
      exit 0
      end
  amt = s.0
  amd = strip(s.amt)'/'
  drop s. e.
  call get_mounted
  hits = 0
  table = 'amt'time(s)
  Address ISPExec
  'tbcreate' table 'names(fs mount) nowrite'
  do i = 1 to s.0
    if wordpos('rw',s.i) = 0 then iterate
    parse value s.i with fs mount .
    cmd = 'automount -f' fs
    x = bpxwunix(cmd,,as.,ae.,env.)
    do ai = 1 to as.0
      if pos('No information',as.ai) > 0 then iterate
      if pos('STATE:',as.ai) = 0 then iterate
      'tbadd' table
      hits = hits + 1
    end
  end
  if hits = 0 then do
     zedsmsg = 'No Automounts'
     zedlmsg = 'No automounted filesystems were detected,' ,
               'are you sure automount is active?'
     'setmsg msg(isrz001)'
     'tbend' table
     end
  else do
       'tbtop' table
       do forever
          zsel = null
          'tbdispl' table 'panel(zfsamnt)'
          if rc > 0 then leave
          if zsel /= 'S' then iterate
          'select pgm(isrdslst) parm(UDL' mount')'
          end
       end
  'tbend' table
  exit

Get_Mounted:
  x = syscalls('ON')
  s.0 = 0
  c = 0
  address syscall
  numeric digits 12
  'getmntent m.'
  do i=1 to m.0
    'statfs' m.mnte_fsname.i 'st.'
    if left(m.mnte_fsname.i,1) = '/' then iterate
    if left(m.mnte_fsname.i,1) = '*' then iterate
    if st.stfs_rdonly = 0 then ro = 'rw'
    else iterate
    mount = m.mnte_path.i
    if left(mount,length(amd)) /= amd then iterate
    zfs   = m.mnte_fsname.i
    c = c + 1
    s.c = strip(zfs) mount ro
    s.0 = c
  end
  x = syscalls('OFF')
  return
